#!/usr/bin/env perl6
#
# A command-line factorization program based Perl 5's ntheory.
#
# Written in Perl 5 in 1999,2000 (c) by Aaron Sherman <ajs@ajs.com>
# Updated to Perl 6 in 2019 (c) by Aaron Sherman <ajs@ajs.com>
#
# Distributed under the Artistic License 2.0 which you should have
# received with your copy of Perl.

# Pull in the Perl 5 ntheory module's "factor"
use Inline::Perl5;
our $p5 = Inline::Perl5.new();
$p5.use('ntheory');

our &factor = $p5.run(q{
    sub factor {
        my $n = shift @_;
        ntheory::factor($n);
    }
    \&factor;
});

sub MAIN(
    *@numbers, #= Your integer inputs here...
    Bool :$test #= Verify the results
) {
    for @numbers -> $number {
        # Force passing $number as a string so that ntheory can instantiate
        # a Perl 5 Math::BigInt with it.
        my @factors = factor(~$number).sort;
        if $test {
            my $product = [*] @factors;
            die "fac($number) != {@factors.join('*')} = $product"
                unless $product == +$number;
            say "Result is correct.";
        }
        say @factors.join(' ');
    }
}
